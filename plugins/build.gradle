buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
    dependencies {
    	// Elasticsearchはmavenのセントラルレポジトリにビルドツールを公開している。
    	// https://mvnrepository.com/artifact/org.elasticsearch.gradle/build-tools
    	classpath group: 'org.elasticsearch.gradle', name: 'build-tools', version: '7.4.2'		
    }
}

plugins {
	id 'java'
	// 依存関係の解決にmavenが必要
	id 'maven'
	// 開発環境としてEclipseを使用する場合には必要
	id 'eclipse'
}

// プラグインをビルドするためのGradleプラグインの名前
apply plugin: 'elasticsearch.esplugin'

sourceCompatibility = 1.8
// ElasticsearchのruntimeがJDK1.8でも動作するようにしておく。
targetCompatibility = 1.8

group = 'taka8.elasticsearch.plugins'
version = '0.0.1'

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

dependencies {
	testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.11'
	testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.11'
}

eclipse {
	 classpath {
		containers.clear()
		downloadSources = true
		downloadJavadoc = true
	}
}

ext {
	// 以下の二つを設定しないとプラグインのビルドがエラーになる
	licenseFile = project.file('LICENSE')
	noticeFile = project.file('NOTICE')
}

esplugin {
	// プラグイン名
	name project.name
	
	// プラグインの説明
	description 'Elasticsearch sample plugin'
	
	// プラグインの入口
	classname 'taka8.elasticsearch.plugins.SamplePlugin'
}

